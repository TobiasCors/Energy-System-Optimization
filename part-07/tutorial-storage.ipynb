{
  "cells": [
    {
      "cell_type": "markdown",
      "metadata": {},
      "source": [
        "# Tutorial VII - Storage Modeling\n",
        "\n",
        "Energy System Optimization with Julia\n",
        "\n",
        "Dr. Tobias Cors (Hamburg University of Applied Science - Summer 2025)\n",
        "\n",
        "# 1. Modeling the Unit Commitment Problem with Storage\n",
        "\n",
        "Implement the Unit Commitment problem with storage from the lecture in\n",
        "Julia. Before we start, let’s load the necessary packages and data."
      ],
      "id": "b16db609-d6a2-4a00-abc0-24086f0882f8"
    },
    {
      "cell_type": "code",
      "execution_count": 1,
      "metadata": {},
      "outputs": [],
      "source": [
        "using JuMP, HiGHS\n",
        "using CSV\n",
        "using DataFrames\n",
        "using CairoMakie\n",
        "using Dates\n",
        "\n",
        "# Set up CairoMakie\n",
        "set_theme!(theme_light())"
      ],
      "id": "4"
    },
    {
      "cell_type": "markdown",
      "metadata": {},
      "source": [
        "## 1.1 Load and Process Data\n",
        "\n",
        "First, we load the data from the CSV files and process them into\n",
        "dictionaries for easy access."
      ],
      "id": "79e18eca-3436-4140-a45b-06a25284b50e"
    },
    {
      "cell_type": "code",
      "execution_count": 1,
      "metadata": {},
      "outputs": [],
      "source": [
        "# Get the directory of the current file\n",
        "file_directory = \"$(@__DIR__)/data\"\n",
        "\n",
        "# Load data\n",
        "dfGenerators = CSV.read(\"$file_directory/generator.csv\", DataFrame)\n",
        "dfStorages = CSV.read(\"$file_directory/storage.csv\", DataFrame)\n",
        "dfWindTurbines = CSV.read(\"$file_directory/windTurbine.csv\", DataFrame)\n",
        "dfScenarios = CSV.read(\"$file_directory/scenario.csv\", DataFrame)\n",
        "\n",
        "# Process generator data\n",
        "dictGenerators = Dict(\n",
        "    row.name => (\n",
        "        min_power = row.min_power,\n",
        "        max_power = row.max_power,\n",
        "        variable_cost = row.variable_cost,\n",
        "        fix_cost = row.fix_cost,\n",
        "        min_up_time = row.min_up_time,\n",
        "        min_down_time = row.min_down_time,\n",
        "        ramp_up = row.ramp_up,\n",
        "        ramp_down = row.ramp_down,\n",
        "        startup_cost = row.startup_cost,\n",
        "        efficiency = row.efficiency\n",
        "    ) for row in eachrow(dfGenerators)\n",
        ")\n",
        "\n",
        "# Process storage data\n",
        "dictStorages = Dict(\n",
        "    row.name => (\n",
        "        min_power = row.min_power,\n",
        "        max_power = row.max_power,\n",
        "        min_energy = row.min_energy,\n",
        "        max_energy = row.max_energy,\n",
        "        charge_efficiency = row.charge_efficiency,\n",
        "        discharge_efficiency = row.discharge_efficiency,\n",
        "        self_discharge_rate = row.self_discharge_rate,\n",
        "        ramp_up = row.ramp_up,\n",
        "        ramp_down = row.ramp_down\n",
        "    ) for row in eachrow(dfStorages)\n",
        ")\n",
        "\n",
        "# Process wind turbine data\n",
        "dictWindTurbines = Dict(\n",
        "    row.name => (\n",
        "        variable_cost = row.variable_cost,\n",
        "    ) for row in eachrow(dfWindTurbines)\n",
        ")\n",
        "\n",
        "# Process scenario data\n",
        "date_format = dateformat\"yyyy-mm-dd HH:MM:SS\"\n",
        "dictScenarios = Dict()\n",
        "for scenario in unique(dfScenarios.scenario)\n",
        "    scenario_data = dfScenarios[dfScenarios.scenario .== scenario, :]\n",
        "    dictScenarios[scenario] = (\n",
        "        datetime = DateTime.(scenario_data.datetime, date_format),\n",
        "        demand_forecast = scenario_data.demand_forecast,\n",
        "        wind_forecast = scenario_data.wind_forecast\n",
        "    )\n",
        "end\n"
      ],
      "id": "6"
    },
    {
      "cell_type": "markdown",
      "metadata": {},
      "source": [
        "## 1.2 Implement the Unit Commitment Model with Storage\n",
        "\n",
        "Now, let’s implement the Unit Commitment model with storage. We’ll\n",
        "create a function that takes the data dictionaries as input and returns\n",
        "the model.\n",
        "\n",
        "**TASK:** Fill out the code below where you see `## YOUR CODE HERE` to\n",
        "implement the storage in the model."
      ],
      "id": "0d863575-536c-432c-842b-381013d0c1a2"
    },
    {
      "cell_type": "code",
      "execution_count": 0,
      "metadata": {},
      "outputs": [],
      "source": [
        "function solve_unit_commitment(dictGenerators, dictStorages, dictWindTurbines, scenario)\n",
        "    # Create model\n",
        "    model = Model(HiGHS.Optimizer)\n",
        "    set_silent(model)\n",
        "\n",
        "    # Define the time periods and sets\n",
        "    T = 1:length(scenario.datetime)  # Time periods (hours)\n",
        "    G = keys(dictGenerators)  # Set of thermal generators\n",
        "    S = keys(dictStorages)    # Set of storage units\n",
        "    W = keys(dictWindTurbines) # Set of wind turbines\n",
        "\n",
        "    # Decision Variables\n",
        "    @variable(model, p[g in G, t in T] >= 0)  # Power output of generator g at time t\n",
        "    @variable(model, u[g in G, t in T], Bin)  # Binary variable for generator status (1=on, 0=off)\n",
        "    @variable(model, v[g in G, t in T], Bin)  # Binary variable for startup (1=startup, 0=no startup)\n",
        "    @variable(model, p_w[w in W, t in T] >= 0)  # Power output of wind at time t\n",
        "    @variable(model, p_fictive[t in T] >= 0)  # Fictive power at time t -> used to model a fictive production in power balance constraint and penalize it with a very high cost in the objective function in case the scenario is not feasible, i.e. not enough generation is available to cover the demand\n",
        "\n",
        "    # Storage variables\n",
        "    @variable(model, p_ch[s in S, t in T] >= 0)     # Charging power of storage s at time t\n",
        "    @variable(model, p_dis[s in S, t in T] >= 0)    # Discharging power of storage s at time t\n",
        "    @variable(model, e[s in S, t in T] >= 0)        # Energy level of storage s at time t\n",
        "    @variable(model, u_ch[s in S, t in T], Bin)     # Binary variable for charging status (1=charging, 0=not charging)\n",
        "    @variable(model, u_dis[s in S, t in T], Bin)    # Binary variable for discharging status (1=discharging, 0=not discharging)\n",
        "\n",
        "    # Objective Function\n",
        "    @objective(model, Min, sum(\n",
        "        dictGenerators[g].variable_cost * p[g,t] +   # Variable cost of production\n",
        "        dictGenerators[g].fix_cost * u[g,t] +        # Fixed cost of running\n",
        "        dictGenerators[g].startup_cost * v[g,t]      # Startup cost of starting the generator\n",
        "        for g in G, t in T\n",
        "    ) + sum(\n",
        "        dictWindTurbines[w].variable_cost * p_w[w,t]    # Variable cost of wind production\n",
        "        for w in W, t in T\n",
        "    ) + sum(\n",
        "        1000 * p_fictive[t]    # Cost of fictive production\n",
        "        for t in T\n",
        "    ))\n",
        "\n",
        "    # Constraints\n",
        "    # Power balance constraint (including storage): Total generation must equal demand\n",
        "    ## YOUR CODE HERE\n",
        "\n",
        "\n",
        "    # Generator limits: Power output must be within min/max when running\n",
        "    @constraint(model, [g in G, t in T],\n",
        "        p[g,t] <= dictGenerators[g].max_power * u[g,t] # Max power when running\n",
        "    )\n",
        "    @constraint(model, [g in G, t in T],\n",
        "        p[g,t] >= dictGenerators[g].min_power * u[g,t] # Min power when running\n",
        "    )\n",
        "\n",
        "    # Wind limits: Wind power cannot exceed forecast\n",
        "    @constraint(model, [w in W, t in T],\n",
        "        p_w[w,t] <= scenario.wind_forecast[t]\n",
        "    )\n",
        "\n",
        "    # Minimum up time: Generator must stay on for minimum duration after startup\n",
        "    @constraint(model, min_up[g in G, t in T],\n",
        "        sum(u[g,τ] for τ in max(1, t-dictGenerators[g].min_up_time+1):t) >= \n",
        "        dictGenerators[g].min_up_time * v[g,t]\n",
        "    )\n",
        "    \n",
        "    # Minimum down time: Generator must stay off for minimum duration after shutdown\n",
        "    @constraint(model, min_down[g in G, t in T],\n",
        "        sum(1 - u[g,τ] for τ in max(1, t-dictGenerators[g].min_down_time+1):t) >= \n",
        "        dictGenerators[g].min_down_time * (1 - u[g,t])\n",
        "    )\n",
        "\n",
        "    # Ramp rate limits: Power change between consecutive timesteps/hours is limited\n",
        "    @constraint(model, [g in G, t in 2:length(T)],\n",
        "        p[g,t] - p[g,t-1] <= dictGenerators[g].ramp_up      # Max ramp up\n",
        "    )\n",
        "    @constraint(model, [g in G, t in 2:length(T)],\n",
        "        p[g,t-1] - p[g,t] <= dictGenerators[g].ramp_down    # Max ramp down\n",
        "    )\n",
        "\n",
        "    # Startup variable definition: v_g[g,t] = 1 if generator g is started at time t\n",
        "    @constraint(model, [g in G, t in 2:length(T)],\n",
        "        v[g,t] >= u[g,t] - u[g,t-1]     # v_g = 1 if u_g changes from 0 (t-1) to 1 (t)\n",
        "    )\n",
        "\n",
        "    # Storage constraints\n",
        "    # Energy balance (Tip: start at t=2)\n",
        "    ## YOUR CODE HERE\n",
        "\n",
        "\n",
        "    # Energy balance at t=1: Initial energy level (assume empty at start) (Tip: define the constraint for e[s,t] at t=1)\n",
        "    ## YOUR CODE HERE\n",
        "\n",
        "\n",
        "    # Energy limits: Energy level must be within min/max\n",
        "    ## YOUR CODE HERE\n",
        "\n",
        "\n",
        "    # Power limits and mutual exclusion: Storage power cannot exceed max power when charging/discharging and charging and discharging cannot happen at the same time\n",
        "    ## YOUR CODE HERE\n",
        "\n",
        "\n",
        "    # Storage ramp rate limits: Power change between consecutive timesteps/hours is limited (Tip: define the constraints starting at t=2)\n",
        "\n",
        "\n",
        "    # Solve the model\n",
        "    optimize!(model)\n",
        "\n",
        "    # Assert that the solution is feasible\n",
        "    if termination_status(model) != MOI.OPTIMAL\n",
        "        ts = termination_status(model)\n",
        "        @info \"Optimization finished. The model was not solved correctly. Termination Status: $ts\"\n",
        "        # Helpful resource: https://jump.dev/JuMP.jl/stable/manual/solutions/#Conflicts\n",
        "    end\n",
        "\n",
        "    # Return results\n",
        "    return (\n",
        "        p_g = value.(p),      # Generator power output\n",
        "        p_w = value.(p_w),    # Wind power output\n",
        "        u_g = value.(u),      # Generator status\n",
        "        v_g = value.(v),      # Startup events\n",
        "        p_ch = value.(p_ch),  # Storage charging power\n",
        "        p_dis = value.(p_dis),# Storage discharging power\n",
        "        e = value.(e),        # Storage energy level\n",
        "        total_cost = objective_value(model)\n",
        "    )\n",
        "end"
      ],
      "id": "8"
    },
    {
      "cell_type": "markdown",
      "metadata": {},
      "source": [
        "## 1.3 Solve and Analyze Results\n",
        "\n",
        "Now, let’s solve the model and analyze the results with simple plotting."
      ],
      "id": "50cfa383-8cfb-4d57-8287-b17c9a2c9e57"
    },
    {
      "cell_type": "code",
      "execution_count": 0,
      "metadata": {},
      "outputs": [],
      "source": [
        "# Create a dataframe to store results\n",
        "results_df = DataFrame(\n",
        "    scenario = String[],        # Scenario identifier\n",
        "    datetime = DateTime[],      # Timestamp\n",
        "    total_cost = Float64[],     # Total system cost\n",
        "    wind_curtailment = Float64[], # Curtailed wind power\n",
        "    thermal_generation = Float64[], # Total thermal generation\n",
        "    wind_generation = Float64[],    # Total wind generation\n",
        "    storage_charge = Float64[],     # Storage charging power\n",
        "    storage_discharge = Float64[],  # Storage discharging power\n",
        "    storage_energy = Float64[]      # Storage energy level\n",
        ")\n",
        "\n",
        "# Loop over scenarios\n",
        "for (scenario_name, scenario_data) in dictScenarios\n",
        "    solution = solve_unit_commitment(dictGenerators, dictStorages, dictWindTurbines, scenario_data)\n",
        "\n",
        "    # Store results for each time period\n",
        "    for t in 1:length(scenario_data.datetime)\n",
        "        push!(results_df, (\n",
        "            scenario_name,\n",
        "            scenario_data.datetime[t],\n",
        "            solution.total_cost,\n",
        "            sum(scenario_data.wind_forecast[t] - solution.p_w[w,t] for w in keys(dictWindTurbines)),\n",
        "            sum(solution.p_g[g,t] for g in keys(dictGenerators)),\n",
        "            sum(solution.p_w[w,t] for w in keys(dictWindTurbines)),\n",
        "            sum(solution.p_ch[s,t] for s in keys(dictStorages)),\n",
        "            sum(solution.p_dis[s,t] for s in keys(dictStorages)),\n",
        "            sum(solution.e[s,t] for s in keys(dictStorages))\n",
        "        ))\n",
        "    end\n",
        "end\n",
        "\n",
        "# Plot generation over time for each scenario\n",
        "for (scenario_name, scenario_data) in dictScenarios\n",
        "    # Create figure with subplots\n",
        "    fig = Figure(size=(1000, 800))\n",
        "    \n",
        "    # Format datetime to show only hours\n",
        "    hours = hour.(results_df.datetime)\n",
        "    \n",
        "    # Generation profile\n",
        "    ax1 = Axis(fig[1, 1], xlabel=\"Hour of Day\", ylabel=\"Power [MW]\")\n",
        "    lines!(ax1, hours, results_df.thermal_generation, label=\"Thermal Generation\")\n",
        "    lines!(ax1, hours, results_df.wind_generation, label=\"Wind Generation\")\n",
        "    lines!(ax1, hours, results_df.wind_curtailment, label=\"Wind Curtailment\")\n",
        "    lines!(ax1, hours, scenario_data.wind_forecast, label=\"Wind Forecast\")\n",
        "    lines!(ax1, hours, scenario_data.demand_forecast, label=\"Demand\")\n",
        "    axislegend(ax1)\n",
        "    ax1.title = \"Generation Profile for Scenario $scenario_name\"\n",
        "    \n",
        "    # Storage energy level\n",
        "    ax2 = Axis(fig[1, 2], xlabel=\"Hour of Day\", ylabel=\"Energy [MWh]\")\n",
        "    lines!(ax2, hours, results_df.storage_energy, label=\"Energy Level\")\n",
        "    axislegend(ax2)\n",
        "    ax2.title = \"Storage Energy Level for Scenario $scenario_name\"\n",
        "    \n",
        "    # Storage power\n",
        "    ax3 = Axis(fig[2, 1], xlabel=\"Hour of Day\", ylabel=\"Power [MW]\")\n",
        "    lines!(ax3, hours, results_df.storage_charge, label=\"Charging\")\n",
        "    lines!(ax3, hours, results_df.storage_discharge, label=\"Discharging\")\n",
        "    axislegend(ax3)\n",
        "    ax3.title = \"Storage Power for Scenario $scenario_name\"\n",
        "    \n",
        "    # Display the figure\n",
        "    display(fig)\n",
        "end\n"
      ],
      "id": "10"
    },
    {
      "cell_type": "markdown",
      "metadata": {},
      "source": [
        "## 1.4 Verify Results"
      ],
      "id": "8ff26ca1-ecd2-4737-b32b-e7542ba422e8"
    },
    {
      "cell_type": "code",
      "execution_count": 0,
      "metadata": {},
      "outputs": [],
      "source": [
        "# Test your answer\n",
        "# Check objective value / total cost is in the correct range\n",
        "@assert isapprox(results_df.total_cost[1], 1.156e6, atol=1e4) \"The total cost for scenario S1 should be 1.156e6 but is $(results_df.total_cost[1])\"\n",
        "println(\"Excellent work! You've successfully implemented the storage model and solved the optimization problem.\")\n"
      ],
      "id": "12"
    },
    {
      "cell_type": "markdown",
      "metadata": {},
      "source": [
        "# Solutions\n",
        "\n",
        "You will likely find solutions to most exercises online. However, I\n",
        "strongly encourage you to work on these exercises independently without\n",
        "searching explicitly for the exact answers to the exercises.\n",
        "Understanding someone else’s solution is very different from developing\n",
        "your own. Use the lecture notes and try to solve the exercises on your\n",
        "own. This approach will significantly enhance your learning and\n",
        "problem-solving skills.\n",
        "\n",
        "Remember, the goal is not just to complete the exercises, but to\n",
        "understand the concepts and improve your programming abilities. If you\n",
        "encounter difficulties, review the lecture materials, experiment with\n",
        "different approaches, and don’t hesitate to ask for clarification during\n",
        "class discussions.\n",
        "\n",
        "Later, you will find the solutions to these exercises online in the\n",
        "associated GitHub repository, but we will also quickly go over them in\n",
        "next week’s tutorial. To access the solutions, click on the Github\n",
        "button on the lower right and search for the folder with today’s lecture\n",
        "and tutorial. Alternatively, you can ask ChatGPT or Claude to explain\n",
        "them to you. But please remember, the goal is not just to complete the\n",
        "exercises, but to understand the concepts and improve your programming\n",
        "abilities."
      ],
      "id": "c6dcd071-b648-4e12-a02e-f385f6257c9d"
    }
  ],
  "nbformat": 4,
  "nbformat_minor": 5,
  "metadata": {
    "kernel_info": {
      "name": "julia"
    },
    "kernelspec": {
      "name": "julia",
      "display_name": "Julia",
      "language": "julia"
    },
    "language_info": {
      "name": "julia",
      "codemirror_mode": "julia",
      "version": "1.11.5"
    }
  }
}